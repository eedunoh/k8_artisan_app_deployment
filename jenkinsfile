pipeline {
    agent any

    // I parametized my terraform workflow
    parameters {
        booleanParam(name: 'TERRAFORM_PLAN', defaultValue: false, description: 'select this option to plan terraform configurations')
        booleanParam(name: 'TERRAFORM_APPLY', defaultValue: false, description: 'select this option to apply terraform configurations')
        booleanParam(name: 'CLUSTER_DEPLOYMENTS', defaultValue: false, description: 'select this option to deploy app and resources to k8 cluster')
        booleanParam(name: 'TERRAFORM_DESTROY', defaultValue: false, description: 'select this option to destroy terraform configurations')
    }

    environment {
        APP_FOLDER = 'cluster_app_and_resources_deployments/app_folder/'
        IMAGE_NAME = 'jackedu/artisan_app'
        IMAGE_TAG = 'latest'
        GIT_URL = 'https://github.com/eedunoh/k8_artisan_app_deployment.git'
        K8_CLUSTER_NAME = 'artisian_app_cluster'
        K8_CLUSTER_REGION = 'eu-north-1'
        MANIFESTS_DIR = 'cluster_app_and_resources_deployments/manifests_yml/'
        TERRAFORM_DIR = 'cluster_app_and_resources_deployments/terraform_files/'
        TF_PLAN_FILE = 'tfplan.binary'
        SONAR_PROJECT_NAME = 'artisan_app_sonar'

        // tool 'sonar-scanner' - is a Jenkins command that means: "Go find where the program named 'sonar-scanner' is installed on this computer.
        SCANNER_HOME = tool 'sonar-scanner'
    }


    stages {

        // Clone the git repository/work area
        stage('Clone Git Repository') {
            steps {
                echo "Cleaning workspace and cloning gir repositoy..."
                deleteDir()
                git branch: 'main', url: "${GIT_URL}"
                sh 'ls -lart'
            }
        }


        // Scans Code for quality, maintainability, bugs, code smells, test coverage
        stage('SonarQube Analysis') {
            steps {
                withSonarQubeEnv('sonar') {
                    sh "$SCANNER_HOME/bin/sonar-scanner -Dsonar.projectName=${SONAR_PROJECT_NAME} -Dsonar.projectKey=${SONAR_PROJECT_NAME}"
                }
            }
        }
        

        // Scans your local source code directory (before you build the image) for security vulnerabilities (CVEs), secrets and misconfigurations.
        stage('Trivy FS Scan') {
            steps {
                sh 'trivy fs --format table -o trivy-fs-report.html .'
            }
        }


        // Build docker image
        stage('Build Docker Image') {
            steps {
                echo "Building Docker image: ${IMAGE_NAME}:${IMAGE_TAG}"
                dir("${APP_FOLDER}") {
                    script {
                        dockerImage = docker.build("${IMAGE_NAME}:${IMAGE_TAG}")
                    }
                }
            }
        }


        // Scans a built Docker image.
        stage('Trivy Image Scan') {
            steps {
                // Generate a readable report (always runs)
                sh "trivy image --format table -o trivy-image-report.html ${IMAGE_NAME}:${IMAGE_TAG}"
                
                
                // Enforce quality gate (fails if CRITICAL/HIGH CVEs found and breaks the pipeline). 
                // N/B:  --exit-code 0 means nothing will happen even if there are vulnerabilities (CVE - Common Vulnerabilities and Exposures). 
                // --exit-code 1 means pipeline will stop.
                sh "trivy image --exit-code 1 --severity CRITICAL,HIGH ${IMAGE_NAME}:${IMAGE_TAG}"
            }
        }


        // Your Trivy scan report will be stored in a file. This stage lets you view your scan report on jenkins UI (or terminal).
        stage('Publish Trivy Image Scan Report') {
            steps {
                publishHTML([
                    reportDir: '.',                      // directory where the HTML file is saved
                    reportFiles: 'trivy-image-report.html',
                    reportName: 'Trivy Image Scan Report',
                    allowMissing: false,
                    keepAll: true,                       // keep old reports from previous builds
                    alwaysLinkToLastBuild: true
                ])
            }
        }

        // Push docker image to docker registry (dockerhub)
        stage('Push Docker Image to Docker Hub') {
            steps {
                echo "üöÄ Pushing Docker image to Docker Hub..."
                script {
                    docker.withRegistry('https://index.docker.io/v1/', 'my_docker_hub_secrets') {
                        dockerImage.push("${IMAGE_TAG}")
                    }
                }
            }
        }
    

        stage('Terraform Init') {
            steps{
                echo "‚öôÔ∏è Initializing Terraform..."
                withCredentials([[
                    $class: 'AmazonWebServicesCredentialsBinding',
                    credentialsId: 'my_aws_credentials'
                ]]) {
                    dir("${TERRAFORM_DIR}") {
                        sh '''
                            echo "======= Terraform Init ======="
                            terraform init
                        '''
                    }
                }
            }
        }


        stage('Terraform Plan') {
            when {
                expression { params.TERRAFORM_PLAN }
            }
            steps {
                echo "üîç Running Terraform Plan..."
                withCredentials([[
                    $class: 'AmazonWebServicesCredentialsBinding',
                    credentialsId: 'my_aws_credentials'
                ]]) {
                    dir("${TERRAFORM_DIR}") {
                        sh """
                            echo "======= Terraform Plan ======="
                            terraform plan -out=${TF_PLAN_FILE}
                            terraform show -no-color ${TF_PLAN_FILE} > tfplan.txt
                        """
                    }
                    archiveArtifacts artifacts: "${TERRAFORM_DIR}/${TF_PLAN_FILE}, ${TERRAFORM_DIR}/tfplan.txt", onlyIfSuccessful: true
                }
            }
        }

        
        stage('Terraform Apply') {
            when {
                expression { params.TERRAFORM_APPLY }
            }
            steps {
                echo "üöÄ Applying Terraform Plan..."
                withCredentials([[
                    $class: 'AmazonWebServicesCredentialsBinding',
                    credentialsId: 'my_aws_credentials'
                ]]) {
                    dir("${TERRAFORM_DIR}") {
                        sh """
                            echo "======= Terraform Apply ======="
                            terraform apply -auto-approve ${TF_PLAN_FILE}
                        """
                    }
                }
            }
        }


        stage('Deploy To Kubernetes Cluster') {
            when {
                expression { params.CLUSTER_DEPLOYMENTS }
            }

            steps {
                echo "üöÄ Deploying application and resources to Kubernetes cluster..."
                withCredentials([[
                    $class: 'AmazonWebServicesCredentialsBinding',
                    credentialsId: 'my_aws_credentials'
                ]]) {
                    dir("${MANIFESTS_DIR}") {
                        sh """
                            # Set up the kubeconfig for the EKS cluster
                            aws eks --region ${K8_CLUSTER_REGION} update-kubeconfig --name ${K8_CLUSTER_NAME}


                            # Check current context
                            echo "Current context after setup:"
                            kubectl config current-context


                            # Now apply the manifests
                            kubectl apply -f .
                        """
                    }
                }
            }
        }        


        stage('Terraform Destroy') {
            when {
                expression { params.TERRAFORM_DESTROY }
            }
            steps {
                echo "üí£ Destroying Terraform Infrastructure..."
                withCredentials([[
                    $class: 'AmazonWebServicesCredentialsBinding',
                    credentialsId: 'my_aws_credentials'
                ]]) {
                    dir("${TERRAFORM_DIR}") {
                        sh """
                            echo "======= Terraform Destroy ======="
                            terraform destroy -auto-approve
                        """
                    }
                }
            }
        }

    }



    post {

        success {
            echo '‚úÖ Pipeline completed successfully!'
        }

        failure {
            echo '‚ùå Pipeline failed!'
        }

        always {
            echo 'üîî Pipeline finished running.'
            cleanWs()  // Optional cleanup of workspace after each run
        }
    }
}